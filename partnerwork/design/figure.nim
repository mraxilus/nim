## Draw a whole picture: two dancers, their connections, and how it moves.
##
##   A static figure is drawn from a canonical pose.  An animated one samples
##     a cycle of poses on one clock -- the bodies carried rigidly by
##     transforms, the reach re-routed each frame with one way round settled
##     for the whole move -- so everything stays in one piece.
##   A state the rules say does not exist is refused, not drawn: `partsOf`
##     asserts `danceable`, so the build fails rather than publishing a wrap
##     that does not wrap (rule 7).
##     Cost of refusing at build time: a page asking for an impossible state
##       dies with an assertion rather than showing a gap.  Accepted -- the
##       gap is the author's decision to make, not the drawing's.

{.experimental: "strictFuncs".}

import std/[algorithm, math, options, sequtils, strformat, strutils]

import ./[body, geometry, pose, route, rules, style]


const
  WIDE* = 160.0    ## The box a picture with captions needs.
  SIZE* = 116.0    ## And the box it needs without them.

type Twists* = array[Arm, float]
  ## How far each connection has wound, in turns: what the reach swings
  ## through (rules 27, 28).  Zero draws straight, a half draws the X, a
  ## whole draws the diamond, a turn and a half the swan.
  ##   It once carried two more channels -- a pigtail at a lone reach's
  ##     middle and a braid across a pair -- which the retired rotation page
  ##     owned.  Rule 16 took the pigtail away, having no ceiling left for
  ##     it to mark, and rule 28's measured winding replaced the braid with
  ##     geometry.  Neither has had a caller since.

const NO_TWIST*: Twists = [0.0, 0.0]
  ## Nothing wound: every figure but the turn pages'.


func twoTone*(runs: seq[Run]; mid: Point; lead_side, foll_side: Arm):
    seq[string] =
  ## Draw one reach in its two hands' own colours, meeting at its middle
  ## point (rule 9).
  ##   Each half is exactly the mark it ends on: the lead's in their arm's
  ##     ink and the deep shade, the follow's in theirs and the plain one.
  ##   So a line draws the pair of colours that names which hands are joined,
  ##     instead of leaving it to two marks that go too small to read; and
  ##     the shade still says which end is whose when both hands share a hue.
  let (near, far) = splitAt(runs, mid)
  @[reachMarkup(near, DEEP[lead_side]), reachMarkup(far, INK[foll_side])]


func ghosts*(holds: Holds; levels: Levels; ways: Ways):
    seq[tuple[who: Dancer, arm: Arm]] =
  ## List every hand that is not where its arm hangs.
  for arm in Arm:
    if holds[arm].isNone:
      continue
    let held = [(Dancer.Lead, arm), (Dancer.Follow, holds[arm].get)]
    for (who, own) in held:
      if slotOf(own, levels[arm], ways[arm]) != (own, Slot.Default):
        result.add (who, own)


func settled*(pose: Pose; holds: Holds; levels: Levels; ways: Ways): Pose =
  ## Get the same pose with every hand put in the slot its hold settles it
  ## in.
  ##   There is nothing to solve: a settled hand is in one of six places, and
  ##     which one is decided by its own side and by the level and way of the
  ##     hold it is part of (rules 2 to 6).
  ##   A hand that is free, or held by a hold that has not said both, stays
  ##     where the arm hangs.  Hands still move smoothly between slots when a
  ##     picture moves; it is the settled state that is discrete.
  var wind: Winds
  for arm in Arm:
    if holds[arm].isNone:
      continue
    wind[Dancer.Lead][arm] = settledWind(arm, levels[arm], ways[arm])
    let own = holds[arm].get
    wind[Dancer.Follow][own] = settledWind(own, levels[arm], ways[arm])
  result = pose
  result.wind = wind


func bodiesOf(pose: Pose): tuple[lead, follow: route.Body] =
  ## See the couple as the routing does.
  ((pose.place[Dancer.Lead], pose.facing[Dancer.Lead]),
   (pose.place[Dancer.Follow], pose.facing[Dancer.Follow]))


func danceable*(pose: Pose; holds: Holds;
    levels: Levels = default(Levels); ways: Ways = default(Ways)): bool =
  ## Test whether every lock and wrap in this hold really is one (rule 7).
  ##   A lock or wrap position may only be used when the line goes round no
  ##     less than just under half the circumference -- it does not mean
  ##     anything to have a wrap without the line actually going round the
  ##     body.  So whether a state exists at all depends on the orientation,
  ##     and this is what says so.
  let
    put = settled(pose, holds, levels, ways)
    p = handsOf(put)
    (lead_body, follow_body) = bodiesOf(put)
  for arm in Arm:
    if holds[arm].isNone:
      continue
    if roundOf(levels[arm], ways[arm]).isNone:
      continue                     # nothing claimed, nothing to hold up
    let ends: route.Ends = (p[Dancer.Lead][arm],
                            p[Dancer.Follow][holds[arm].get],
                            lead_body, follow_body)
    if not wrapsEnough(ends, levels[arm], ways[arm]):
      return false
  true


func clearingMarks*(put: Pose; a, b: Point): seq[Mark] =
  ## List what a settled reach between these two hands has to keep out of
  ## (rule 22).
  ##   Every hand mark but the two it joins -- a line through one says that
  ##     hand is held -- and both chevrons, which a line through hides.
  ##   Each clearance is what that mark actually reaches, so the daylight is
  ##     seen and not guessed; and this is the one list, read by the drawing
  ##     and by the check that measures it.
  ##   A hand is one disc, because a hand's mark is compact.  A chevron is a
  ##     thin V, so it is a string of small discs walked along its own two
  ##     legs -- a single disc over the whole of it would cover the middle
  ##     of the body and push every reach outside it.
  let p = handsOf(put)
  for who in Dancer:
    let drawn = chevronPoints(put.place[who], put.facing[who])
    for leg in 0 .. 1:
      for step in 0 .. CHEVRON_STEPS:
        let part = float(step) / float(CHEVRON_STEPS)
        result.add ((drawn[leg].x + (drawn[leg + 1].x - drawn[leg].x) * part,
                     drawn[leg].y + (drawn[leg + 1].y - drawn[leg].y) * part),
                    CHEVRON_CLEAR)
    for side in Arm:
      let q = p[who][side]
      if min(dist(q, a), dist(q, b)) > 0.01:
        result.add (q, (if who == Dancer.Lead: LEAD_CLEAR else: FOLLOW_CLEAR))


func axisOf*(put: Pose): tuple[along, across: Point, bearing: float] =
  ## Get the pair's own axis: the way from the lead to the follow, the way
  ## square to it, and the bearing of the first.
  let
    lead = put.place[Dancer.Lead]
    follow = put.place[Dancer.Follow]
    span = max(dist(lead, follow), 1e-9)
    along: Point = ((follow.x - lead.x) / span, (follow.y - lead.y) / span)
  (along, (-along.y, along.x), bearing(follow.x - lead.x, follow.y - lead.y))


func windOf*(put: Pose; holds: Holds; arm: Arm): tuple[phi, spread: float] =
  ## Measure how far one connection has wound, in degrees (rules 27, 28).
  ##   Each held hand sits on its own body's rim, and both bodies stand on
  ##     the pair's axis, so the angle a hand makes with that axis is what
  ##     going round means here.  Where the two ends make the same angle
  ##     the pair is unwound; the difference between them is the wind.
  ##   Measured, never handed in: a drawing told how far it has wound can
  ##     be told wrong, and has been twice: an orbit that keeps its bearing
  ##     turns nobody and so winds nothing (rule 28), and an orbit that
  ##     keeps its side to the centre winds as far as it carries (rule 32).
  ##     Measuring survived both answers without being touched.
  let
    axis = axisOf(put)
    p = handsOf(put)
    a = p[Dancer.Lead][arm]
    b = p[Dancer.Follow][holds[arm].get]
    phi_a = bearing(a.x - put.place[Dancer.Lead].x,
                    a.y - put.place[Dancer.Lead].y) - axis.bearing
    phi_b = bearing(b.x - put.place[Dancer.Follow].x,
                    b.y - put.place[Dancer.Follow].y) - axis.bearing
  (phi_a, wrap180(phi_b - phi_a))


func partsOf*(pose: Pose; holds: Holds; levels: Levels = default(Levels);
    over = none(Arm); free = Free.Fade; captions = true;
    ways: Ways = default(Ways); twist: Twists = NO_TWIST;
    clear_marks = false): seq[string] =
  ## Draw every element of one pose, in the order the picture is read from.
  ##   `clear_marks` asks a straight reach to bend round the marks it does
  ##     not join (rule 22).  It is off by default so that only the pages
  ##     that have been looked at under the rule take it: the frame page's
  ##     straight and wrapping reaches are the next piece of that work, and
  ##     this flag is where it will be turned on for them.
  # A lock or wrap that does not go round the body is not one, and a state
  # that cannot be danced is an edge that is not drawn -- so this refuses
  # rather than drawing something the rules say does not exist.
  doAssert danceable(pose, holds, levels, ways),
    &"Undanceable state asked for; got `{holds}` at `{levels}`, `{ways}`."
  # Every drawing path comes through here, so this is where the hands are
  # put: a pose handed in ready-made is settled exactly like one built below.
  let
    put = settled(pose, holds, levels, ways)
    p = handsOf(put)
    (lead_body, follow_body) = bodiesOf(put)

  var bits = @[ringOf(put), border(put, Dancer.Lead),
               border(put, Dancer.Follow)]
  for who in Dancer:
    bits.add chevron(put.place[who], put.facing[who])
  # Where each displaced hand came from, as a grey outline of its own mark.
  # `back` carries locks, so a hand no longer says by where it sits how far
  # it has been taken -- the ghost of the place it left says it instead, and
  # a hand still at home has no ghost to confuse it with.
  for (who, arm) in ghosts(holds, levels, ways):
    let q = handPoint(put.place[who], put.facing[who], arm)
    bits.add hand(q.x, q.y, who == Dancer.Lead, arm, held = false,
                  free = Free.Grey)
  # A wound pair crosses: once by a half turn, twice by a whole one, with
  # the X or the diamond that makes (rules 27, 28).
  let winding = holds[Arm.L].isSome and holds[Arm.R].isSome and
    abs(twist[Arm.L]) > 1e-9
  # And a wound pair says which way it wound by which arm it keeps on top,
  # so the wind names the over-arm rather than the caller saying it twice.
  let on_top = if not winding: over else: some(overArm(twist[Arm.L]))
  var routes: array[Arm, seq[Point]]
  for arm in Arm:
    if holds[arm].isNone:
      continue
    let ends: route.Ends = (p[Dancer.Lead][arm],
                            p[Dancer.Follow][holds[arm].get],
                            lead_body, follow_body)
    if levels[arm] == some(Level.Above):
      # Over the head: nothing is in the way from above, and the wind is
      # said by crossing rather than by hugging (rules 1 and 14).
      routes[arm] =
        if winding:
          # The pose says where the hands are and which way the arm went
          # round; the wind says how far.  A half turn's sweep is the pose's
          # own -- the hands really have swapped sides -- and a whole turn's
          # is a full round on top of it.
          wound(ends.a, ends.b, axisOf(put).across,
                degToRad(windOf(put, holds, arm).phi),
                2 * PI * twist[arm], share = windShare(twist[arm], arm))
        elif clear_marks:
          clearedReach(ends.a, ends.b, clearingMarks(put, ends.a, ends.b))
        else:
          straightReach(ends.a, ends.b)
    else:
      # What the hold says, if it says anything; the short way if not.
      routes[arm] = routed(ends, wayFor(ends, levels[arm], ways[arm])).get.pts
  # A wound pair meets more than once, and a rope alternates: each strand
  # dives under at every second crossing.  So the crossings are found once,
  # in order along the line, and shared out between the two arms.
  var dives: array[Arm, seq[Point]]
  if winding:
    let meetings = crossingsOf(routes[Arm.L], routes[Arm.R])
    for i, meeting in meetings:
      # The arm named `over` stays on top at the first meeting, so it is
      # the other one that dives there, and they swap at each one after --
      # which is what makes a box a twist and not an overlap (rule 27).
      let under = if (i mod 2 == 0) == (on_top == some(Arm.L)): Arm.R
                  else: Arm.L
      dives[under].add meeting

  let order = if on_top == some(Arm.L): [Arm.R, Arm.L] else: [Arm.L, Arm.R]
  for arm in order:
    if holds[arm].isSome:
      let
        pts = routes[arm]
        runs =
          if winding: cutGapsAt(pts, dives[arm])
          elif on_top == some(other(arm)): cutGap(pts, routes[other(arm)])
          else: @[pts]
      bits.add twoTone(runs, pts[pts.len div 2], arm, holds[arm].get)
  for arm in Arm:
    let q = p[Dancer.Lead][arm]
    bits.add hand(q.x, q.y, true, arm, holds[arm].isSome, levels[arm], free)
  for own in [Arm.R, Arm.L]:
    let
      q = p[Dancer.Follow][own]
      by = Arm.toSeq.filterIt(holds[it] == some(own))
      level = if by.len > 0: levels[by[0]] else: none(Level)
    bits.add hand(q.x, q.y, false, own, by.len > 0, level, free)
  if captions:
    for arm in Arm:
      bits.add caption(put.place[Dancer.Lead], put.facing[Dancer.Lead], arm,
                       (if arm == Arm.L: "Left" else: "Right"),
                       put.wind[Dancer.Lead][arm])
    for own in [Arm.R, Arm.L]:
      bits.add caption(put.place[Dancer.Follow], put.facing[Dancer.Follow],
                       own, handName(own), put.wind[Dancer.Follow][own])
  bits.filterIt(it.len > 0)


func extent*(pose: Pose; captions = true): float =
  ## Measure how far this pose reaches from the origin, ring and captions
  ## included.
  let edge = if captions: CAPTION_R + 20 else: BODY_R + R + 2
  for who in Dancer:
    result = max(result, hypot(pose.place[who].x, pose.place[who].y) + edge)
  if pose.ring.isSome:
    let (centre, radius) = pose.ring.get
    result = max(result, hypot(centre.x, centre.y) + radius + 4)


func view*(half: float): string =
  ## Write the square viewBox a figure fills.
  &"""viewBox="{n(-half)} {n(-half)} {n(2 * half)} {n(2 * half)}""""


func frame*(cls: string; holds: Holds; levels: Levels = default(Levels);
    over = none(Arm); lead_turn = 0.0; follow_turn = 0.0; free = Free.Fade;
    captions = true; pose = none(Pose); half = none(float);
    ways: Ways = default(Ways); twist: Twists = NO_TWIST;
    clear_marks = false): string =
  ## Draw one picture, canonical unless a pose is handed in already turned.
  let
    drawn = if pose.isSome: pose.get
            else: canonicalise(spinAbout(
              spinAbout(rest(), Dancer.Lead, lead_turn),
              Dancer.Follow, follow_turn))
    box = if half.isSome: half.get
          else: (if captions: WIDE else: SIZE) / 2
    bits = partsOf(drawn, holds, levels, over, free, captions, ways, twist,
                   clear_marks)
  &"""<svg class="{cls}" {view(box)}>""" & "\n        " &
    bits.join("\n        ") & "\n      </svg>"


func series*(steps: seq[float]): string =
  ## Say one animated value's frames on one clock.
  steps.mapIt(n(it)).join(";")

func series*(steps: seq[string]): string =
  ## Say one animated pair's frames on one clock, already written out.
  steps.join(";")


func beat*(t: float): string =
  ## Write one moment of an animation's clock, finely enough to keep the
  ## frames in order.
  ##   Not `n`, which writes a tenth: a whole move is one unit long here, so
  ##     a tenth would collapse whole stages of it into the same instant.
  let written = formatFloat(t, ffDecimal, 4)
  result = written.strip(leading = false, chars = {'0'})
                  .strip(leading = false, chars = {'.'})
  if result.len == 0:
    result = "0"


func keyed*(times: seq[float]; count: int): string =
  ## Say when each frame of an animation is due, where they are not evenly
  ## spread (rule 26).
  ##   Evenly spread is what a browser assumes, so nothing is written for
  ##     it: a move that spends its clock evenly says so by saying nothing,
  ##     and only a move that ranks its stages carries the extra attribute.
  if times.len != count or count < 2:
    return ""
  for i, t in times:
    if abs(t - float(i) / float(count - 1)) > 1e-9:
      return &""" keyTimes="{times.mapIt(beat(it)).join(";")}""""
  ""


func animate*(attr: string; steps: seq[float]; dur: float;
    times: seq[float] = @[]): string =
  ## Animate one attribute over the cycle.
  &"""<animate attributeName="{attr}" values="{series(steps)}"""" &
    keyed(times, steps.len) &
    &""" dur="{dur}s" repeatCount="indefinite"/>"""


func paired*(markup, inner: string): string =
  ## Reopen a self-closing element so it can carry its own animations.
  let tag = markup[1 ..< markup.find(' ')]
  markup[0 .. ^3] & ">" & inner & "</" & tag & ">"


const LONG_ENOUGH = 999.0
  ## A dash long enough to be the rest of any reach on any page.

const GAPS_DRAWN = 2
  ## Breaks a moving reach has room for, whether it uses them or not.
  ##   Two, because a swan crosses three times and the alternation puts two
  ##     of those on one connection (rule 31).  Every frame writes the same
  ##     number of them, which is what lets the pattern be animated at all.


func dashedAt*(pts: seq[Point]; dives: seq[float]; starts = 0.0):
    tuple[pattern, offset: string] =
  ## Say a moving reach's break as a dash pattern: how far it runs, how long
  ## the break is, and then the rest of it (rule 29).
  ##   A still reach is cut into runs where it dives under its partner, but
  ##     the number of runs changes with the number of crossings and a path
  ##     that changes shape cannot be morphed between frames.  A dash puts
  ##     the same break in the same place and leaves the path one piece.
  ##   Where this half of the reach has no crossing in it the break is zero
  ##     long, which draws as no break at all -- so every frame says three
  ##     numbers whether it is broken or not, and nothing jumps.
  ##   A crossing that sits on the join between the two halves breaks both
  ##     of them, each by as much of the gap as falls inside it, so the
  ##     break slides across the middle instead of hopping.
  ##   Which is why a dive is given as how far along the *whole* reach it
  ##     lies, with `starts` saying where this half begins: how near a
  ##     crossing is in the plane says nothing about where on the line it
  ##     falls once the line snakes back past itself (rule 31), and taking
  ##     nearness for position drew breaks where there was no crossing.
  ##   The pattern is written a gap longer than it needs and started a gap
  ##     in, which comes to the same line and keeps the first dash off
  ##     zero: a zero-length dash under a round cap is drawn as a dot, and
  ##     a break that begins at a half's own start would leave one.
  ##   Room for `GAPS_DRAWN` of them, always written and mostly zero long:
  ##     a swan crosses three times and the alternation puts two of those on
  ##     one connection (rule 31), while a frame has none at all, and the
  ##     markup has to say the same number of things either way.
  var runs = @[0.0]
  for i in 0 ..< pts.high:
    runs.add runs[^1] + dist(pts[i], pts[i + 1])
  # Where along this half each break falls, in order, so a pattern reads
  # from one end to the other.  The gap is centred on the crossing and
  # clipped to this half's own ends, which is what lets it cross the join
  # without flickering; a crossing outside this half leaves nothing.
  var breaks: seq[tuple[opens, shuts: float]]
  for dive in dives:
    let
      opens = clamp(dive - starts - BREAK / 2, 0.0, runs[^1])
      shuts = clamp(dive - starts + BREAK / 2, 0.0, runs[^1])
    if shuts > opens:
      breaks.add (opens, shuts)
  breaks = breaks.sortedByIt(it.opens)
  # Run, gap, run, gap: one pair per break the pattern has room for.
  var
    lens: seq[float]
    at = 0.0
  for k in 0 ..< GAPS_DRAWN:
    let
      gap = if k < breaks.len: max(breaks[k].shuts - breaks[k].opens, 0.0)
            else: 0.0
      # A break there is none of is parked a whole line past the end, so
      # the run before it is long rather than nothing: a zero-length dash
      # under a round cap is drawn as a dot, and an unused gap must leave
      # no mark at all.
      opens = if gap > 0: max(breaks[k].opens, at) else: at + LONG_ENOUGH
    lens.add opens - at
    lens.add gap
    at = opens + gap
  # The pattern is written a gap longer at the front and started a gap in,
  # which comes to the same line and keeps the first dash off zero.
  var says = @[n(lens[0] + lens[1])]
  for k in 1 ..< lens.len:
    says.add n(lens[k])
  says.add n(LONG_ENOUGH)
  (says.join(" "), n(lens[1]))


func facings*(poses: seq[Pose]; who: Dancer): seq[float] =
  ## Get a dancer's facing through a cycle, continuous so it turns the way
  ## it turned.
  ##   Wrapped angles step from 179 to -179 at a half turn and are read as
  ##     most of a turn the other way -- a body spinning backwards while its
  ##     own hands, placed absolutely, travel the right way.
  continuous(poses.mapIt(it.facing[who]))


func animatedPoses*(cls: string; holds: Holds; walk: seq[Pose];
    half = none(float); levels: Levels = default(Levels);
    ways: Ways = default(Ways); dur = 9.6;
    times: seq[float] = @[]; wound = 0.0): string =
  ## Draw one picture moving through a walk of poses handed in.
  ##   Every moving figure comes through here, whether its walk is a whole
  ##     move's cycle or one edge of a state graph rocked back and forth.
  ##   `times` says when each frame is due, for a move that ranks its own
  ##     stages (rule 26); without it the frames are evenly spread, which
  ##     is what a browser does anyway.
  ##   `wound` says how far the pair was already wound at the first frame.
  ##     What the drawing can measure for itself is how far the wind
  ##     *changes*, since a whole turn puts every hand back where it was --
  ##     so the whole turns already in the hold are the one thing a walk has
  ##     to be told (rule 28).
  let
    poses = walk.mapIt(settled(it, holds, levels, ways))
    box = if half.isSome: half.get
          else: poses.mapIt(extent(it, captions = false)).max
    hands = poses.mapIt(handsOf(it))

  var bits: seq[string]
  # The ring is drawn only where one is happening.  A move nobody orbits in
  # has no ring at all, rather than a ring of no radius: the mark says
  # *somebody is going round somebody*, and a mark that is always there,
  # invisible, says it of every move.
  if poses.anyIt(it.ring.isSome):
    var ring_cx, ring_cy, ring_r: seq[float]
    for p in poses:
      let ring = p.ring.get((centre: (0.0, 0.0), radius: 0.0))
      ring_cx.add ring.centre.x
      ring_cy.add ring.centre.y
      ring_r.add ring.radius
    bits.add paired(
      &"""<circle cx="0" cy="0" r="0" fill="none" stroke="{QUIET}"""" &
        """ stroke-width="1" stroke-dasharray="3 4"/>""",
      animate("cx", ring_cx, dur, times) & animate("cy", ring_cy, dur, times) &
        animate("r", ring_r, dur, times))

  # A body is rigid: only where it is and which way it faces ever change.  So
  # it is drawn once, at the origin facing up, and carried about by a pair of
  # transforms -- which is exact, and spares the markup a boundary per frame.
  for who in Dancer:
    var still = rest()
    still.place[who] = (0.0, 0.0)
    still.facing[who] = 0.0
    still.wind[who] = poses[0].wind[who]
    let places = poses.mapIt(&"{n(it.place[who].x)} {n(it.place[who].y)}")
    bits.add "<g>" &
      """<animateTransform attributeName="transform" type="translate"""" &
      &""" values="{series(places)}"""" & keyed(times, poses.len) &
      &""" dur="{dur}s" repeatCount="indefinite"/>""" &
      """<animateTransform attributeName="transform" type="rotate"""" &
      &""" additive="sum" values="{series(facings(poses, who))}"""" &
      keyed(times, poses.len) &
      &""" dur="{dur}s" repeatCount="indefinite"/>""" &
      border(still, who) & chevron(still.place[who], still.facing[who]) &
      "</g>"

  # One reach per frame, every frame the same number of points, and -- this
  # is the whole of it -- **one way round both bodies for the entire move**
  # (rule 1).  What a browser draws between two sample frames is the two
  # reaches blended point by point, so two neighbouring frames that disagree
  # about which side of a body the line passes are drawn, in between, as a
  # line sweeping straight through that body.  Only an `above` connection may
  # ever do that.  Settling the way round once, before any frame is routed,
  # makes the disagreement impossible rather than unlikely.  The halves are
  # split at a fixed index, which the even resampling makes the middle of the
  # line, so both shades morph as one shape.
  #
  # Both arms are routed before either is drawn, because a crossing is a
  # fact about the two of them together and each has to know where it dives
  # under the other (rule 29).
  var
    routes: array[Arm, seq[seq[Point]]]
    winds: array[Arm, seq[float]]
  for arm in Arm:
    if holds[arm].isNone:
      continue
    let site = holds[arm].get
    var frames: seq[route.Ends]
    for i, h in hands:
      frames.add (h[Dancer.Lead][arm], h[Dancer.Follow][site],
                  (poses[i].place[Dancer.Lead],
                   poses[i].facing[Dancer.Lead]),
                  (poses[i].place[Dancer.Follow],
                   poses[i].facing[Dancer.Follow]))
    if levels[arm] == some(Level.Above):
      # Over the head: from above nothing is in the way, and nothing hugs
      # (rules 1, 14).
      # A pair winds as it turns, and the drawing follows rather than being
      # told (rule 28).  How far it has wound is measured on every frame
      # and read as one continuous turning, so a reach that has gone right
      # round is not mistaken for one that has not moved -- and so nothing
      # jumps between two frames.
      if holds[other(arm)].isSome:
        let seen = continuous(poses.mapIt(windOf(it, holds, arm).spread))
        # Measured from where it started, and started from where the
        # hold says: the two together are the whole of the winding.
        winds[arm] = seen.mapIt(it + 360 * wound - seen[0])
        for i, f in frames:
          routes[arm].add wound(f.a, f.b, axisOf(poses[i]).across,
                                degToRad(windOf(poses[i], holds, arm).phi),
                                degToRad(winds[arm][i]),
                                share = windShare(winds[arm][i] / 360, arm))
      else:
        routes[arm] = frames.mapIt(straightReach(it.a, it.b))
    else:
      # A hold that says which way round says it for every frame at once: a
      # slot is fixed relative to its facing, so the direction is too.
      # Where it says nothing, one way is picked for the whole move instead.
      let
        said = wayFor(frames[0], levels[arm], ways[arm])
        way = if said.isSome: said.get else: oneWayRound(frames)
      routes[arm] = frames.mapIt(routed(it, some(way)).get.pts)

  # Where the pair crosses, one of them dives, and it is the same one the
  # still figure breaks: the crossings in order along the reach, the diving
  # arm alternating from the first, and the first named by the sign of the
  # wind (rule 29).  A moving reach cannot be cut into runs -- the number of
  # them would change from frame to frame and a path that changes shape
  # cannot morph -- so it keeps its one piece and wears the break as a dash.
  # A swan crosses three times, so an arm can dive more than once and every
  # crossing is kept rather than only the first (rule 31).
  # Kept as how far along its own reach each dive lies, not as where it is
  # on the page: a snake passes near its own line again further along.
  var dives: array[Arm, seq[seq[float]]]
  if holds[Arm.L].isSome and holds[Arm.R].isSome:
    for i in 0 ..< poses.len:
      var mine: array[Arm, seq[float]]
      let
        turned_by = if winds[Arm.L].len == poses.len: winds[Arm.L][i] else: 0.0
        on_top = overArm(turned_by)
      for k, meeting in crossingsOf(routes[Arm.L][i], routes[Arm.R][i]):
        let under = if (k mod 2 == 0) == (on_top == Arm.L): Arm.R else: Arm.L
        mine[under].add alongAt(routes[under][i], meeting)
      for arm in Arm:
        dives[arm].add mine[arm]

  for arm in Arm:
    if holds[arm].isNone:
      continue
    let
      site = holds[arm].get
      middle = routes[arm][0].len div 2
    for (ink, lo, hi) in [(DEEP[arm], 0, middle), (INK[site], middle,
                          routes[arm][0].high)]:
      var
        paths: seq[string]
        dashes, offsets: seq[string]
      for i, pts in routes[arm]:
        let part = pts[lo .. hi]
        paths.add smoothed(part)
        if dives[arm].len == poses.len:
          let dashed = dashedAt(part, dives[arm][i],
                                starts = polylineLen(pts[0 .. lo]))
          dashes.add dashed.pattern
          offsets.add dashed.offset
      bits.add paired(
        &"""<path d="{paths[0]}" fill="none" stroke="{ink}"""" &
          (if dashes.len == 0: ""
           else: &""" stroke-dasharray="{dashes[0]}"""" &
             &""" stroke-dashoffset="{offsets[0]}"""") &
          &""" stroke-width="{LINK_W}" stroke-linecap="round"""" &
          """ stroke-linejoin="round"/>""",
        &"""<animate attributeName="d" values="{series(paths)}"""" &
          keyed(times, poses.len) &
          &""" dur="{dur}s" repeatCount="indefinite"/>""" &
          (if dashes.len == 0: ""
           else: &"""<animate attributeName="stroke-dasharray"""" &
             &""" values="{dashes.join(";")}"""" & keyed(times, poses.len) &
             &""" dur="{dur}s" repeatCount="indefinite"/>""" &
             &"""<animate attributeName="stroke-dashoffset"""" &
             &""" values="{offsets.join(";")}"""" & keyed(times, poses.len) &
             &""" dur="{dur}s" repeatCount="indefinite"/>"""))

  # A moving hand says its level, as a still one does (rule 21) -- and it
  # has to say it the same way all the way through.
  #   So a hand is drawn once at the origin and *carried* by a transform,
  #     exactly as a body is, rather than animating its own coordinates.
  #     The `above` hatch is a pattern anchored to user space, so a mark
  #     that slides through user space slides across a hatch that stands
  #     still, and the fill swims about inside its own outline.  Carried by
  #     a transform, the hatch is carried with it and holds its place.
  #   It also lets the mark keep its own dot rather than having one
  #     animated alongside it: a group can hold two elements where
  #     `paired` reopens one.
  func carried(mark: string; pts: seq[Point]): string =
    let places = pts.mapIt(xy(it))
    "<g>" &
      """<animateTransform attributeName="transform" type="translate"""" &
      &""" values="{series(places)}"""" & keyed(times, pts.len) &
      &""" dur="{dur}s" repeatCount="indefinite"/>""" & mark & "</g>"

  for sd in Arm:
    bits.add carried(hand(0, 0, true, sd, holds[sd].isSome, levels[sd]),
                     hands.mapIt(it[Dancer.Lead][sd]))
  for own in [Arm.R, Arm.L]:
    let
      by = Arm.toSeq.filterIt(holds[it] == some(own))
      held = by.len > 0
    bits.add carried(
      hand(0, 0, false, own, held,
           (if held: levels[by[0]] else: none(Level))),
      hands.mapIt(it[Dancer.Follow][own]))
  &"""<svg class="{cls}" {view(box)}>""" & "\n        " &
    bits.join("\n        ") & "\n      </svg>"


func animated*(cls: string; holds: Holds; move: MoveApply;
    half = none(float); levels: Levels = default(Levels);
    ways: Ways = default(Ways); dur = 9.6; samples = 14): string =
  ## Draw the same picture, moving: stage one travels, stage two comes home.
  animatedPoses(cls, holds, cycle(move, samples), half, levels, ways, dur)
